(uiop:define-package #:the-great-rouclere/tests
  (:use #:cl)
  (:local-nicknames (#:a #:alexandria-2)
                    (#:d #:drakma)
                    (#:h #:hunchentoot)
                    (#:r #:the-great-rouclere))
  (:export #:*debugp* #:magic!))

(in-package #:the-great-rouclere/tests)

(defvar *debugp* nil)

(defun magic! (&optional (debugp *debugp*))
  (let ((5am:*on-error* (if debugp :debug nil))
        (5am:*on-failure* (if debugp :debug nil)))
    (5am:run! 'the-great-rouclere)))

(5am:def-suite the-great-rouclere)

(5am:in-suite the-great-rouclere)

(defun fail (&rest args)
  (5am:fail "Test failure: ~S" args))

(defun arrange-basics (&optional (url "/ping"))
  (r:expect (:get url)
    (r:with :header "Magic-Dust" "Imagination")
    (r:with :accept "application/magic-show")
    (r:answer (h:+http-ok+)
      (r:with :body "That's perfect!!!")
      (r:with :content-type "text/magical")
      (r:with :header "Magic-Dust" "Prestidigitation"))))

(5am:test basics
  (r:with-magic-show (port)
    ;; Arrange
    (arrange-basics)
    ;; Act
    (multiple-value-bind (body status-code headers)
        (d:http-request (format nil "http://localhost:~D/ping" port)
                        :accept "application/magic-show"
                        :additional-headers '(("Magic-Dust" . "Imagination")))
      ;; Assert
      (5am:is (equal "That's perfect!!!" body))
      (5am:is (= h:+http-ok+ status-code))
      (5am:is (eql 0 (search "text/magical" (a:assoc-value headers :content-type))))
      (5am:is (equal "Prestidigitation" (a:assoc-value headers :magic-dust))))))

(5am:test expectations-single
  (r:with-magic-show (port :on-letdowns #'fail :on-surprises #'fail)
    ;; Arrange
    (arrange-basics)
    ;; Assert
    (5am:is (= 1 (length (r:expectations port))))
    (let ((expectation (pop (r:expectations port))))
      (5am:is (eq :get (getf expectation :method)))
      (5am:is (= 1 (getf expectation :times)))
      (5am:is (equal "/ping" (getf expectation :url)))
      (5am:is (a:set-equal '(("Magic-Dust" . "Imagination")
                             ("Accept" . "application/magic-show"))
                           (getf expectation :headers)
                           :test #'equal))
      (5am:is (equal '() (getf expectation :accept)))
      (let ((answer (getf expectation :answer)))
        (5am:is (= h:+http-ok+ (getf answer :code)))
        (5am:is (equal "That's perfect!!!" (getf answer :body)))
        (5am:is (a:set-equal '(("Magic-Dust" . "Prestidigitation")
                               ("Content-Type" . "text/magical"))
                             (getf answer :headers)
                             :test #'equal))))
    (5am:is (= 0 (length (r:expectations port))))))

(5am:test expectations-multiple
  (flet ((test (calls)
           (r:with-magic-show (port :on-letdowns #'fail :on-surprises #'fail)
             ;; Arrange
             (r:expect (:get "/ping"))
             (r:expect (:get "/pong"))
             (r:expect (:get "/pung"))
             ;; Assert
             (5am:is (= 3 (length (r:expectations port))))
             (let ((expectation (first (r:expectations port))))
               (5am:is (equal "/ping" (getf expectation :url))))
             (let ((expectation (second (r:expectations port))))
               (5am:is (equal "/pong" (getf expectation :url))))
             (let ((expectation (third (r:expectations port))))
               (5am:is (equal "/pung" (getf expectation :url))))
             (flet ((test (path)
                      (let* ((url (format nil "http://localhost:~D~A" port path))
                             (status-code (nth-value 1 (d:http-request url))))
                        (5am:is (= h:+http-ok+ status-code)))))
               (mapc #'test calls))
             (5am:is (= 0 (length (r:expectations port)))))))
    (let ((calls '("/ping" "/pong" "/pung")))
      (test calls)
      (test (reverse calls)))))

(5am:test letdown
  (let ((flag nil))
    (flet ((on-letdowns (expectations)
             (setf flag t)
             (5am:is (= 1 (length expectations)))
             (let ((expectation (first expectations)))
               (5am:is (eq :get (getf expectation :method)))
               (5am:is (= 1 (getf expectation :times)))
               (5am:is (equal "/nowhere" (getf expectation :url))))))
      (let ((string (with-output-to-string (*debug-io*)
                      (r:with-magic-show (port :on-letdowns #'on-letdowns :on-surprises #'fail)
                        (r:expect (:get "/nowhere"))))))
        (5am:is-true flag)
        (5am:is (eql 0 (search ";; The Great Rouclere still has 1 unmet expectations!" string)))))))

(5am:test surprise
  (let ((flag nil)
        expected-port)
    (flet ((on-surprises (surprises)
             (setf flag t)
             (5am:is (= 1 (length surprises)))
             (destructuring-bind (actual-port request expectations) (first surprises)
               (5am:is (eql expected-port actual-port))
               (5am:is (eq :get (h:request-method request)))
               (5am:is (string= "/nowhere" (h:request-uri request)))
               (5am:is (null expectations)))))
      (let ((string (with-output-to-string (*debug-io*)
                      (r:with-magic-show (port :on-surprises #'on-surprises :on-letdowns #'fail)
                        (setf expected-port port)
                        (let* ((url (format nil "http://localhost:~D/nowhere" port)))
                          (multiple-value-bind (body status-code headers uri stream must-close reason)
                              (d:http-request url)
                            (declare (ignore headers uri stream must-close))
                            (5am:is (= r:+http-magic-is-gone+ status-code))
                            (5am:is (string= "Magic Is Gone" reason))
                            (eql 0 (search ";; The Great Rouclere is surprised by this request!" body))
                            (5am:is (= 1 (length (r:surprises port))))
                            (let ((surprise (first (r:surprises port))))
                              (destructuring-bind (request expectations) surprise
                                (5am:is (eq :get (h:request-method request)))
                                (5am:is (string= "/nowhere" (h:request-uri request)))
                                (5am:is (null expectations))))))))))
        (5am:is-true flag)
        (5am:is (eql 0 (search ";; The Great Rouclere has been surprised 1 times!" string)))))))

(5am:test permanent-expectations
  (let ((flag nil))
    (flet ((on-letdowns (expectations)
             (declare (ignore expectations))
             (setf flag t)))
      (r:with-magic-show (port :on-letdowns #'on-letdowns :on-surprises #'fail)
        (r:expect (:get "/always" :times t)
          (r:answer (h:+http-accepted+)))
        (5am:is (= 1 (length (r:expectations port))))
        (5am:is (loop with url = (format nil "http://localhost:~D/always" port)
                      repeat 100
                      for status-code = (nth-value 1 (d:http-request url))
                      always (= status-code h:+http-accepted+)))
        (5am:is (= 1 (length (r:expectations port)))))
      (5am:is (null flag)))))

(5am:test multimagic
  (r:with-magic-show ((port-1 port-2 port-3) :on-letdowns #'fail :on-surprises #'fail)
    (flet ((spell (port url code)
             (r:with-wand-pointed-at (port)
               (r:expect (:get url)
                 (r:answer (code)))))
           (test (port url code)
             (let* ((url (format nil "http://localhost:~D~A" port url))
                    (status-code (nth-value 1 (d:http-request url))))
               (5am:is (= code status-code)))))
      (let ((set-1 (list port-1 "/abra" h:+http-accepted+))
            (set-2 (list port-2 "/kadabra" h:+http-gone+))
            (set-3 (list port-3 "/alakazam" h:+http-bad-gateway+)))
        (apply #'spell set-1)
        (apply #'spell set-2)
        (apply #'spell set-3)
        (apply #'test set-2)
        (apply #'test set-3)
        (apply #'test set-1)))))

(5am:test variables-predicates-side-effects
  (let (result)
    (r:with-magic-show (port :on-letdowns #'fail :on-surprises #'fail)
      (r:expect (:get "/foo/:bar/baz/:quux/:frob")
        (r:with :predicate (lambda () (string= "123" (r:var "bar"))))
        (r:answer (h:+http-ok+)
          (r:with :side-effects
            (lambda () (setf result (list (r:var "quux") (r:var "frob")))))))
      (let* ((url (format nil "http://localhost:~D/foo/123/baz/456/789" port))
             (status (nth-value 1 (d:http-request url))))
        (5am:is (= h:+http-ok+ status))
        (5am:is (equal '("456" "789") result))))))
